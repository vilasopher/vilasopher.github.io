<!DOCTYPE html>
<html>
  <head>
    <script type="text/javascript" src="https://cindyjs.org/dist/latest/Cindy.js"></script>
    <script type="text/javascript" src="https://cindyjs.org/dist/latest/CindyGL.js"></script>
    <title>XY Model</title>
  </head>

	<body style="margin:0;padding:0;overflow:hidden;">
    <div id="CSCanvas"></div>

    <script id="csinit" type="text/x-cindyscript">
      use("CindyGL");

      corners = screenbounds();
      W = dist(corners_1, corners_2) / 2; 
      H = dist(corners_1, corners_4) / 2;
      temp = 1/2;
      invrate = 0.05;
      
      L = [0, 0]; //bottom left corner
      R = [W, 0]; //bottom right corner

      pressed = false;

      createimage("state", W, H);

      randomOnCircle() := (
        t = random();
        (0.5*(cos(2*pi*t)+1), 0.5*(sin(2*pi*t)+1), 0);
      );

      colorplot(L, R, "state", randomOnCircle());

      get(x, y) := imagergb(L, R, "state", (mod(x,W), mod(y,H)));

      shiftedDotProduct(a,b) := (2*a_1-1) * (2*b_1-1) + (2*a_2-1) * (2*b_2-1);

      energy(x, y, spin) := - (shiftedDotProduct(spin, get(x,y+1)) + shiftedDotProduct(spin, get(x,y-1)) + shiftedDotProduct(spin, get(x+1,y)) + shiftedDotProduct(spin, get(x-1,y)));

      newstate(x, y) := (
        current = get(x, y);
        proposal = randomOnCircle();

        currentEnergy = energy(x, y, current);
        proposalEnergy = energy(x, y, proposal);

        if (random() < exp((-1/temp)*(proposalEnergy - currentEnergy)), proposal, current);
      );

      textbox = polygon([(50,50), (50,80), (260,80), (260,50)]);

      PUhue(t) := (
        r = -23898.593840393547 * t^10 + 117840.35887237563 * t^9 - 244636.3064394972 * t^8 + 277415.9549344232 * t^7 - 186334.33037767682 * t^6 + 75048.05514468961 * t^5 - 17453.649022789545 * t^4 + 2141.2320717567013 * t^3 - 126.00916454658359 * t^2 + 3.2707862475631697 * t^1 + 0.9494362769759878 * t^0;
        g = -29724.112795253768 * t^10 + 149336.5674568973 * t^9 - 315877.46819151303 * t^8 + 364972.1032512114 * t^7 - 249897.1036370729 * t^6 + 102839.82349269387 * t^5 - 24626.40697240402 * t^4 + 3165.789507838831 * t^3 - 198.172832167599 * t^2 + 8.98827033248276 * t^1 + 0.16989948030877564 * t^0;
        b = -17706.816770775426 * t^10 + 88694.82390325959 * t^9 - 186850.89832638184 * t^8 + 214715.55235954086 * t^7 - 145935.47337490247 * t^6 + 59494.552912407395 * t^5 - 14118.21450657179 * t^4 + 1808.7134561225141 * t^3 - 102.96016955366275 * t^2 + 0.7481161620030783 * t^1 + 0.07957706120221039 * t^0;
        (r,g,b);
      );

      colorfunction(spin) := (
        x = 2*spin_1-1;
        y = 2*spin_2-1;
        t = (arctan2(x,y)+pi)/(2*pi);
        // TODO: perceptually uniformize the color space!
        // colors_(t * 256 + 1); // WHY DOESN'T THIS WORK??????
        (-1,0,0);
        PUhue(t);
      );
    </script>

    <script id="csdraw" type="text/x-cindyscript">
      repeat(20,
        colorplot(L, R, "state",
          if (random() < invrate, newstate(#.x, #.y), get(#.x, #.y))
        );
      );
      colorplot(colorfunction(imagergb("state", #)));

      if (pressed, (
          temp = max([0.000001,(mouse().y / H) * 2]);
          fill(textbox, color->(1,1,1));
          drawtext((60, 60), "temperature = " + temp);
        );
      );
    </script>

    <script id="csmousedown" type="text/x-cindyscript">
      pressed = true;
    </script>

    <script id="csmouseup" type="text/x-cindyscript">
      pressed = false;
    </script>

    <script type="text/javascript">
    CindyJS({
      scripts: "cs*",
      autoplay: true,
      ports: [{
        id: "CSCanvas",
        width: window.innerWidth,
        height: window.innerHeight,
        transform: [{
          visibleRect: [0, 0, window.innerWidth, window.innerHeight]
        }]
      }]
    });
    </script>
  </body>
</html>